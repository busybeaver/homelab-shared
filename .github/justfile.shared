default_tools := "just lefthook dprint act wget earthly"
brew_env_vars := "HOMEBREW_NO_ANALYTICS=1 HOMEBREW_NO_INSECURE_REDIRECT=1 HOMEBREW_CASK_OPTS=--require-sha"

# install all required tooling for development (osx only); the optional additional_tools parameter allows to specify additional tools that should be installed
install *additional_tools:
  #!/usr/bin/env bash
  set -euo pipefail
  IFS=$'\n\t'
  if [[ "{{os()}}" != "macos" ]]; then
    echo "This command currently only works on macOS. On other systems, please install the following tools manually: {{default_tools}} {{additional_tools}}"
    exit -1
  fi

  {{brew_env_vars}} brew install {{default_tools}} {{additional_tools}}

  # alternatives:
  # - https://github.com/cocogitto/cocogitto
  # - https://github.com/its-danny/koji
  # - https://github.com/mainrs/git-cm
  VERSION=0.0.5
  if [[ "{{arch()}}" == "x86_64" ]]; then
    # Intel processors
    FILENAME="comet-darwin-amd64"
  else
    # Apple processors like M1
    FILENAME="comet-darwin-arm64"
  fi

  wget -cO - "https://github.com/liamg/comet/releases/download/v${VERSION-}/${FILENAME-}" > /tmp/comet
  chmod +x /tmp/comet
  sudo mv /tmp/comet /usr/local/bin/comet
  sudo chown root: /usr/local/bin/comet

# uninstall all required tooling for development (osx only); the optional additional_tools parameter allows to specify additional tools that should be uninstalled
uninstall *additional_tools:
  #!/usr/bin/env bash
  set -euo pipefail
  IFS=$'\n\t'
  if [[ "{{os()}}" != "macos" ]]; then
    echo "This command currently only works on macOS. On other systems, please uninstall the following tools manually: {{default_tools}} {{additional_tools}}"
    exit -1
  fi

  {{brew_env_vars}} brew uninstall {{default_tools}} {{additional_tools}}

  sudo rm -f /usr/local/bin/comet

# initializes the tooling for working with this repository
initialize:
  lefthook install && earthly bootstrap

# formats files according to the used standards and rules; if the optional files parameter is provided, only the specified files are formatted; else all files are formatted
format *files:
  dprint fmt {{files}}

# checks if the files comply to the used standards and rules; if the optional files parameter is provided, only the specified files are checked; else all files are checked
check *files:
  dprint check {{files}}

# assisted conventional commits with git
commit *args:
  comet {{args}}

# runs the CI workflows locally; the optional args parameter allows to add additional optional arguments
ci +args='push':
  act {{args}}
