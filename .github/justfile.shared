default_tools := "just lefthook dprint act httpie jq yq"

# install all required tooling for development (osx only)
install *additional_tools:
  {{ if os() != "macos" { error("This command currently only works on macOS. On other systems, please install the following tools manually: " + default_tools + " " + additional_tools) } else { "" } }}
  brew install {{default_tools}} {{additional_tools}}

# initializes the tooling for working with this repository
initialize:
  lefthook install

# formats files according to the used standards and rules
format *files:
  dprint fmt {{files}}

# checks if the files comply to the used standards and rules
check *files:
  dprint check {{files}}

# runs the CI workflows locally
ci +args='push':
  act {{args}}
